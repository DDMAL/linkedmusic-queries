# define input:inference 'urn:owl.ccmusicrules'

# [method 1]
# PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
# PREFIX gn: <http://www.geonames.org/ontology#>
# construct {
#     ?MusicType bf:place ?ParentPlace .
# }
# where {
#     ?MusicType bf:place ?Place .
#     ?Place gn:parentFeature* ?ParentPlace
# }

# [method 2]
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
construct {
    ?MusicTypeOrInstrument bf:place ?ParentPlace ;
                           rdfs:label ?MusicTypeOrInstrument_Label .
    ?ParentPlace rdfs:label ?ParentPlaceLabel .
}
where {
    ?MusicTypeOrInstrument bf:place ?Place ;
                           rdfs:label ?MusicTypeOrInstrument_Label .
    ?Place gn:parentFeature* ?ParentPlace .
    ?ParentPlace rdfs:label ?ParentPlaceLabel .
}

# [method 3: not relying on inference]
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
construct {
    ?MusicTypeOrInstrument bf:place ?ParentPlace_town, ?ParentPlace_county, ?ParentPlace_city , ?ParentPlace_province ;
                           rdfs:label ?MusicTypeOrInstrument_Label .
}
where {
    ?MusicTypeOrInstrument bf:place ?Place ;
                           rdfs:label ?MusicTypeOrInstrument_Label .
          {?Place gn:parentADM4 ?ParentPlace_town . # eg:<https://lib.ccmusic.edu.cn/data/place/131024-101000-liangjiawucun> gn:parentADM4 <https://lib.ccmusic.edu.cn/data/place/131024-101000> .
           ?ParentPlace_town gn:parentADM3 ?ParentPlace_county . # the current database doesn't have `gn:parentADM3` property
           ?ParentPlace_county gn:parentADM2 ?ParentPlace_city . 
           ?ParentPlace_city gn:parentADM1 ?ParentPlace_province} # ?Place is a village
    union {?Place gn:parentADM3 ?ParentPlace_county . 
           ?ParentPlace_county gn:parentADM2 ?ParentPlace_city . 
           ?ParentPlace_city gn:parentADM1 ?ParentPlace_province} # ?Place is a town
    union {?Place gn:parentADM2 ?ParentPlace_city . 
           ?ParentPlace_city gn:parentADM1 ?ParentPlace_province} # ?Place is a county
    union {?Place gn:parentADM1 ?ParentPlace_province} # ?Place is a city
}